source: orders is duckdb.table('data/orders.csv') extend {
    measure: total_revenue is sum(price * quantity)
    measure: avg_order_value is avg(price * quantity)
    measure: no_of_orders is count(order_id)
    measure: total_quantity is sum(quantity)

    view: Everything is {
      select: *
    }
    # big_value
    view: metrics is {
        aggregate:
            # big_value { sparkline=order_count_trend }
            no_of_orders,
            # currency=usd1m
            # description = "Total revenue from all orders"
            # big_value { sparkline=revenue_trend }
            total_revenue,
            # currency=usd1
            # big_value { sparkline=avg_order_value_trend }
            avg_order_value,
            # big_value { sparkline=total_quantity_trend }
            total_quantity
        # bar_chart {size=spark}
        # hidden
        nest: revenue_trend is {
            group_by: sale_date.month
            # currency=usd1m
            aggregate: monthly_revenue is total_revenue
            order_by: sale_date
            limit: 12
        }
        # bar_chart {size=spark}
        # hidden
        nest: order_count_trend is {
            group_by: sale_date.month
            aggregate: monthly_orders is no_of_orders
            order_by: sale_date
            limit: 12
        }
        # bar_chart {size=spark}
        # hidden
        nest: avg_order_value_trend is {
            group_by: sale_date.month
            # currency=usd1
            aggregate: monthly_aov is avg_order_value
            order_by: sale_date
            limit: 12
        }
        # bar_chart {size=spark}
        # hidden
        nest: total_quantity_trend is {
            group_by: sale_date.month
            aggregate: monthly_quantity is total_quantity
            order_by: sale_date
            limit: 12
        }
    }
    # dashboard
    view: Overview is {
        group_by: `Sales Year` is sale_date.year
        nest: metrics
        order_by: `Sales Year` desc
    }

    view: `Product categories` is {
        group_by: category
        aggregate:
            no_of_orders,
            total_revenue
        # bar_chart
        nest: by_products is {
            group_by: product_name
            aggregate:
                no_of_orders,
                total_revenue
        }
    }

    # bar_chart
    view: `Customer countries` is {
        group_by: customer_country
        aggregate:
            no_of_orders,
            total_revenue
    }
}
source: contracts is duckdb.table('data/contracts.csv') extend {

}

query: price_summary is orders -> {
    select:
        price
}

run: orders -> {
    select:
        order_id,
        product_name,
        price,
        quantity,
        customer_country
    where:
        price > 100
    order_by:
        price desc
}
